apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

dependencies {
  compile      project(':denominator-core')
  compile      project(':providers:denominator-dynect')
  compile      project(':providers:denominator-ultradns')
  compile      project(':providers:denominator-route53')
  // to quiet error messages, not as we are using it
  compile     'org.slf4j:slf4j-jdk14:1.7.2'
  compile     'io.airlift:airline:0.5'
}

// create a self-contained jar that is executable
// the file output name is "build/denominator"
task fat(dependsOn: classes, type: Jar) { 
  classifier = "fat"
  destinationDir = buildDir
  archiveName = "denominator"  

  // denominator providers are looked up via service loader
  // normally a fat jar will truncate these.  While we could
  // use fancy groovy to try and combine them dynamically,
  // it is much more straight-forward to hard-code the list.
  doFirst {
    def providerDir = "${buildDir}/provider"
    new File(providerDir).delete()
    def providerFile = new File(providerDir, "META-INF/services/denominator.Provider")
    providerFile.parentFile.mkdirs()
    providerFile << """\
denominator.mock.MockProvider
denominator.dynect.DynECTProvider
denominator.route53.Route53Provider
denominator.ultradns.UltraDNSProvider
"""
    from(providerDir)  
  }

  // grab project compile and runtime deps into the jar
  doFirst {
    // Delay evaluation until the compile configuration is ready
    from {
      configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
      }
    }
    def runtimeDeps = configurations.runtime.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }

  // grab the classes representing the cli
  from (sourceSets*.output.classesDir)

  // make the fat jar a really executable jar
  // http://skife.org/java/unix/2011/06/20/really_executable_jars.html

  manifest {
    attributes 'Main-Class': 'denominator.cli.Denominator'
    attributes("Implementation-Title": "Denominator", "Specification-Version": version, "Implementation-Version": version)
  }

  doLast {
    def srcFile = new File("${destinationDir}/${archiveName}")
    def tmpFile = new File(srcFile.getAbsolutePath() + ".tmp")
    tmpFile.delete()
    tmpFile << "#!/usr/bin/env sh\n"
    tmpFile << 'exec java -jar $0 "$@"' + "\n"
    tmpFile << srcFile.bytes
    tmpFile.setExecutable(true, true)
    tmpFile.renameTo(srcFile)
  }
}

artifacts {
    archives fat
}
